apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'submodule-cfg-plugin'

submoduleConfig { cfg ->
    cfg.enableBaseModuleCfg()
    cfg.enableFlavorsCfg()
    cfg.enableOptimizeCodeCfg()
    cfg.enableDataBindingCfg()
    cfg.enableLintCfg()
    cfg.enableBuildConfigGeneration()
    cfg.enableResValuesInjecting()
    if (rootProject.ext.ciBuild) {
        cfg.enableJacocoCfg()
    }
}

android {
    namespace "ru.tensor.sbis.event_bus"

    def appId = "${rootProject.ext.applicationId}"
    defaultConfig {
        vectorDrawables.useSupportLibrary = true

        // добавлен buildConfigField, т.к. существует проблема с импортом генерируемых ресурсов
        buildConfigField "String", "SETTINGS_OPEN_ACTION", "\"${appId}.SETTINGS\""
        resValue "string", "settings_open_action", "${appId}.SETTINGS"
    }

    buildTypes {
        debug {
            def mainAppId = "$appId${rootProject.ext.applicationDebugSuffix}"
            buildConfigField "String", "FILE_AUTHORITY", "\"${mainAppId}.fileprovider\""
        }
        release {
            def mainAppId = appId
            buildConfigField "String", "FILE_AUTHORITY", "\"${mainAppId}.fileprovider\""
        }
    }

    testOptions.unitTests {
        includeAndroidResources = true
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat 'full'
            }
        }
    }

    android.sourceSets {
        debug {
            java {
                srcDirs = ['src/debug/java']
            }
        }
        release {
            java {
                srcDirs = ['src/release/java']
            }
        }
        main.res.srcDirs = [
                'src/main/res',
        ]
    }
}

dependencies {
    implementation project(':plugin_struct')

    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$rootProject.ext.androidLifecycleReactivestreams"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    testImplementation project(':testing')
}